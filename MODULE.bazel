module(
    name = "everest",
)

bazel_dep(name = "aspect_rules_js", version = "1.42.3")

npm = use_extension("@aspect_rules_js//npm:extensions.bzl", "npm", dev_dependency = True)
npm.npm_translate_lock(
    name = "npm",
    npmrc = "//:.npmrc",
    pnpm_lock = "//:pnpm-lock.yaml",
    verify_node_modules_ignored = "//:.bazelignore",
)
use_repo(npm, "npm")

bazel_dep(name = "aspect_rules_ts", version = "2.4.1")

rules_ts_ext = use_extension("@aspect_rules_ts//ts:extensions.bzl", "ext", dev_dependency = True)
rules_ts_ext.deps()
use_repo(rules_ts_ext, "npm_typescript")

bazel_dep(name = "bazel_skylib", version = "1.6.1")
bazel_dep(name = "buildifier_prebuilt", version = "6.4.0")
bazel_dep(name = "fmt", version = "10.2.1.bcr.1")
bazel_dep(name = "gflags", version = "2.2.2")
bazel_dep(name = "googleapis", version = "0.0.0-20240326-1c8d509c5")
bazel_dep(name = "googletest", version = "1.14.0.bcr.1")
bazel_dep(name = "grpc", version = "1.62.1")
bazel_dep(name = "grpc-java", version = "1.62.2")
bazel_dep(name = "rules_cc", version = "0.0.9")
bazel_dep(name = "rules_cue", version = "0.5.0")
bazel_dep(name = "rules_foreign_cc", version = "0.10.1")
bazel_dep(name = "rules_go", version = "0.47.1", repo_name = "io_bazel_rules_go")
bazel_dep(name = "rules_rust", version = "0.44.0")

# rules_rust 0.44.0 does not work with protobuf 23.1 and this breaks `bazel test src/rust/...`
single_version_override(
    module_name = "protobuf",
    version = "21.7",
)

rust = use_extension("@rules_rust//rust:extensions.bzl", "rust")
rust.toolchain(
    edition = "2021",
    versions = ["1.78.0"],
)
use_repo(rust, "rust_toolchains")

register_toolchains("@rust_toolchains//:all")

crate = use_extension("@rules_rust//crate_universe:extension.bzl", "crate")
crate.from_cargo(
    name = "crates",
    cargo_lockfile = "//:Cargo.lock",
    manifests = [
        "//:Cargo.toml",
        "//src/rust/grpc_server:Cargo.toml",
        "//src/rust/hello_world:Cargo.toml",
        "//src/rust/luhn:Cargo.toml",
        "//src/rust/num:Cargo.toml",
        "//src/rust/web:Cargo.toml",
    ],
)
use_repo(crate, "crates")

bazel_dep(name = "gazelle", version = "0.36.0", repo_name = "bazel_gazelle")

go_sdk = use_extension("@io_bazel_rules_go//go:extensions.bzl", "go_sdk")
go_sdk.download(version = "1.22.2")
go_sdk.nogo(nogo = "@//tools/nogo")

go_deps = use_extension("@bazel_gazelle//:extensions.bzl", "go_deps")
go_deps.from_file(go_mod = "//:go.mod")
go_deps.gazelle_override(
    directives = [
        "gazelle:go_naming_convention go_default_library",
    ],
    path = "github.com/google/cel-go",
)
go_deps.gazelle_override(
    directives = [
        # The following should not be required after the next gazelle release given
        # https://github.com/bazelbuild/bazel-gazelle/commit/b3cddc07edbede6dd7a5c311aac7ac06660aecf0
        "gazelle:proto disable",
    ],
    path = "google.golang.org/protobuf",
)
use_repo(
    go_deps,
    "com_github_antlr4_go_antlr_v4",
    "com_github_aristanetworks_goarista",
    "com_github_cockscomb_cel2sql",
    "com_github_google_cel_go",
    "com_github_google_go_cmp",
    "com_github_google_go_github_v62",
    "com_github_google_uuid",
    "com_github_gosuri_uiprogress",
    "com_github_grpc_ecosystem_go_grpc_middleware_v2",
    "com_github_honeycombio_honeycomb_opentelemetry_go",
    "com_github_honeycombio_otel_config_go",
    "com_github_keybase_go_keychain",
    "com_github_mndrix_tap_go",
    "com_github_mvdan_sh",
    "com_github_pkg_browser",
    "com_github_prometheus_client_golang",
    "com_github_prometheus_client_model",
    "com_github_prometheus_common",
    "com_github_rs_zerolog",
    "com_github_stretchr_testify",
    "com_github_whilp_git_urls",
    "com_github_zenazn_goji",
    "io_gorm_driver_sqlite",
    "io_gorm_gorm",
    "io_opentelemetry_go_contrib_instrumentation_google_golang_org_grpc_otelgrpc",
    "org_golang_google_genproto_googleapis_api",
    "org_golang_google_grpc",
    "org_golang_google_protobuf",
    "org_golang_x_tools",
    "tech_einride_go_aip",
)

bazel_dep(name = "rules_haskell", version = "0.19")

haskell_toolchains = use_extension("@rules_haskell//extensions:haskell_toolchains.bzl", "haskell_toolchains")
haskell_toolchains.bindists(
    # Pick a version from https://www.haskell.org/ghc/download.html that matches the LTS snapshot below.
    version = "9.6.4",
)

stack_snapshot = use_extension("@rules_haskell//extensions:stack_snapshot.bzl", "stack_snapshot")
use_repo(stack_snapshot, "stackage")
stack_snapshot.snapshot(
    # Pick an LTS from https://www.stackage.org/snapshots
    name = "lts-22.11",
)

[
    stack_snapshot.package(
        name = pkg,
    )
    for pkg in [
        "base",
        "HUnit",
        "optparse-applicative",
    ]
]

stack_snapshot.stack_snapshot_json(
    label = "//third_party/haskell:stackage_snapshot.json",
)

bazel_dep(name = "rules_java", version = "7.5.0")
bazel_dep(name = "rules_jni", version = "0.9.1")
bazel_dep(name = "rules_jvm_external", version = "6.1")

JUNIT_VERSION = "5.8.1"

JUNIT_PLATFORM_VERSION = "1.8.1"

maven = use_extension("@rules_jvm_external//:extensions.bzl", "maven")
maven.install(
    artifacts = [
        "com.google.googlejavaformat:google-java-format:1.7",
        "commons-cli:commons-cli:1.4",
        "org.junit.jupiter:junit-jupiter-api:" + JUNIT_VERSION,
        "org.junit.jupiter:junit-jupiter-engine:" + JUNIT_VERSION,
        "org.junit.platform:junit-platform-launcher:" + JUNIT_PLATFORM_VERSION,
        "org.junit.platform:junit-platform-reporting:" + JUNIT_PLATFORM_VERSION,
        "org.junit.vintage:junit-vintage-engine:" + JUNIT_VERSION,
    ],
    fail_if_repin_required = True,
    lock_file = "//third_party/java:maven_install.json",
    repositories = [
        "https://jcenter.bintray.com/",
        "https://maven.google.com",
        "https://repo1.maven.org/maven2",
    ],
)
use_repo(maven, "maven", "unpinned_maven")

bazel_dep(name = "rules_kotlin", version = "1.9.5")
bazel_dep(name = "rules_nodejs", version = "6.1.0")

node = use_extension("@rules_nodejs//nodejs:extensions.bzl", "node")
node.toolchain(
    name = "nodejs",
    node_version = "20.10.0",
)

bazel_dep(name = "rules_oci", version = "1.7.5")

oci = use_extension("@rules_oci//oci:extensions.bzl", "oci")
oci.pull(
    name = "distroless_base",
    digest = "sha256:ccaef5ee2f1850270d453fdf700a5392534f8d1a8ca2acda391fbb6a06b81c86",
    image = "gcr.io/distroless/base",
    platforms = [
        "linux/amd64",
        "linux/arm64",
    ],
)
use_repo(oci, "distroless_base")

bazel_dep(name = "rules_perl", version = "0.2.0")
bazel_dep(name = "rules_pkg", version = "0.10.1")
bazel_dep(name = "rules_proto", version = "6.0.0")
bazel_dep(name = "rules_python", version = "0.31.0")

python_version = "3.12"

python = use_extension("@rules_python//python/extensions:python.bzl", "python")
python.toolchain(python_version = python_version)

pip = use_extension("@rules_python//python/extensions:pip.bzl", "pip")
pip.parse(
    hub_name = "pip",
    python_version = python_version,
    requirements_lock = "//third_party/python:requirements_lock.txt",
)
use_repo(pip, "pip")
